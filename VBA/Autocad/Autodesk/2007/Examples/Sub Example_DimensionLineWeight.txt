Sub Example_DimensionLineWeight()
    ' This example creates a Dim3PointAngular object in model space
    ' and changes the dimension lineweight to a thicker value
    ' using the DimensionLineWeight property
    
    Dim DimPointAngularObj As AcadDim3PointAngular
    Dim AngleVertex(0 To 2) As Double
    Dim FirstPoint(0 To 2) As Double, SecondPoint(0 To 2) As Double
    Dim TextPoint(0 To 2) As Double
    Dim oldLWSetting As Boolean
    Dim CurrentSetting As String
        
    ' Turn on lineweight display so the changes can be seen
    oldLWSetting = ThisDrawing.preferences.LineWeightDisplay
    ThisDrawing.preferences.LineWeightDisplay = True
    
    ' Define the new Dim3PointAngular object
    AngleVertex(0) = 0: AngleVertex(1) = 0: AngleVertex(2) = 0
    FirstPoint(0) = 2: FirstPoint(1) = 2: FirstPoint(2) = 0
    SecondPoint(0) = 1: SecondPoint(1) = 4: SecondPoint(2) = 0
    TextPoint(0) = 6: TextPoint(1) = 6: TextPoint(2) = 0

    ' Create the new Dim3PointAngular object in model space
    Set DimPointAngularObj = ThisDrawing.ModelSpace.AddDim3PointAngular(AngleVertex, FirstPoint, SecondPoint, TextPoint)
    ThisDrawing.Application.ZoomAll

    ' Read and display current dimension lineweight
    CurrentSetting = DimPointAngularObj.DimensionLineWeight
    Select Case CurrentSetting
        Case acLnWtByLayer: CurrentSetting = "By Layer"
        Case acLnWtByBlock: CurrentSetting = "By Block"
        Case acLnWtByLwDefault: CurrentSetting = "By Default"
        Case Else
    End Select

    MsgBox "The dimension lineweight setting for this object is currently set to: " & CurrentSetting

    ' Change the dimension lineweight
    DimPointAngularObj.DimensionLineWeight = acLnWt100
    ThisDrawing.Regen acAllViewports
    
    ' Read and display current dimension lineweight
    MsgBox "The dimension lineweight setting for this object is now set to: " & DimPointAngularObj.DimensionLineWeight
    
    ' Return previous lineweight setting
    ThisDrawing.preferences.LineWeightDisplay = oldLWSetting

End Sub
