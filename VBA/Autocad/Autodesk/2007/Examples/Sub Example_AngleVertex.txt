Sub Example_AngleVertex()
    ' This example creates a Dim3PointAngular object in model space
    ' and then alters its angle vertex
    
    Dim DimPointAngularObj As AcadDim3PointAngular
    Dim AngleVertex(0 To 2) As Double, NewAngleVertex(0 To 2) As Double
    Dim FirstPoint(0 To 2) As Double, SecondPoint(0 To 2) As Double
    Dim TextPoint(0 To 2) As Double
    Dim CurrentVertex As Variant
    
    ' Define the new Dim3PointAngular object
    AngleVertex(0) = 0: AngleVertex(1) = 0: AngleVertex(2) = 0
    NewAngleVertex(0) = 1: NewAngleVertex(1) = 1: NewAngleVertex(2) = 0
    FirstPoint(0) = 2: FirstPoint(1) = 2: FirstPoint(2) = 0
    SecondPoint(0) = 1: SecondPoint(1) = 4: SecondPoint(2) = 0
    TextPoint(0) = 6: TextPoint(1) = 6: TextPoint(2) = 0

    ' Create the new Dim3PointAngular object in model space
    Set DimPointAngularObj = ThisDrawing.ModelSpace.AddDim3PointAngular(AngleVertex, FirstPoint, SecondPoint, TextPoint)
    ThisDrawing.Application.ZoomAll

    ' Display current vertex
    CurrentVertex = DimPointAngularObj.AngleVertex
    MsgBox "The angle vertex of the new object is set to:" & vbCrLf & _
            CurrentVertex(0) & vbCrLf & _
            CurrentVertex(1) & vbCrLf & _
            CurrentVertex(2)

    ' Alter vertex setting for this object
    DimPointAngularObj.AngleVertex = NewAngleVertex
    ThisDrawing.Regen acAllViewports
    
    ' Display new vertex settings
    CurrentVertex = DimPointAngularObj.AngleVertex
    MsgBox "The angle vertex of the object has been reset to:" & vbCrLf & _
            CurrentVertex(0) & vbCrLf & _
            CurrentVertex(1) & vbCrLf & _
            CurrentVertex(2)
    
End Sub
