Sub Example_DimensionLine1Suppress()
    ' This example creates a Dim3PointAngular object in model space
    ' and suppresses the dimension lines using the DimensionLine1Suppress
    ' and DimensionLine2Suppress properties
    
    Dim DimPointAngularObj As AcadDim3PointAngular
    Dim AngleVertex(0 To 2) As Double
    Dim FirstPoint(0 To 2) As Double, SecondPoint(0 To 2) As Double
    Dim TextPoint(0 To 2) As Double
    Dim Line1Suppressed As String, Line2Suppressed As String
    
    ' Define the new Dim3PointAngular object
    AngleVertex(0) = 0: AngleVertex(1) = 0: AngleVertex(2) = 0
    FirstPoint(0) = 2: FirstPoint(1) = 2: FirstPoint(2) = 0
    SecondPoint(0) = 1: SecondPoint(1) = 4: SecondPoint(2) = 0
    TextPoint(0) = 6: TextPoint(1) = 6: TextPoint(2) = 0

    ' Create the new Dim3PointAngular object in model space
    Set DimPointAngularObj = ThisDrawing.ModelSpace.AddDim3PointAngular(AngleVertex, FirstPoint, SecondPoint, TextPoint)
    ThisDrawing.Application.ZoomAll

    ' Read and display current line suppression information
    Line1Suppressed = IIf(DimPointAngularObj.DimLine1Suppress, "is suppressed", "is not suppressed")
    Line2Suppressed = IIf(DimPointAngularObj.DimLine2Suppress, "is suppressed", "is not suppressed")
    
    MsgBox "Line one of the dimension " & Line1Suppressed & vbCrLf & _
           "Line two of the dimension " & Line2Suppressed

    ' Suppress line one of the dimension
    DimPointAngularObj.DimLine1Suppress = True
    ThisDrawing.Regen acAllViewports
    MsgBox "Line one of the dimension is now suppressed"
    
    ' Suppress line two of the dimension
    DimPointAngularObj.DimLine2Suppress = True
    ThisDrawing.Regen acAllViewports
    MsgBox "Line two of the dimension is now suppressed"
   
   ' Turn both dimension lines back on
    DimPointAngularObj.DimLine1Suppress = False
    DimPointAngularObj.DimLine2Suppress = False
    ThisDrawing.Regen acAllViewports
    MsgBox "Both dimension lines are now visible again"

End Sub
