Sub Example_Dock()
    ' This example creates a new toolbar called TestToolbar and inserts three
    ' buttons into it. The toolbar is then displayed as a floating toolbar,
    ' and then as a docked toolbar.
    ' To remove the toolbar after execution of this macro, use the Customize Menu
    ' option from the Tools menu.
    
    Dim currMenuGroup As acadMenuGroup
    Set currMenuGroup = ThisDrawing.Application.MenuGroups.Item(0)
    
    ' Create the new toolbar
    Dim newToolBar As AcadToolbar
    Set newToolBar = currMenuGroup.Toolbars.Add("TestToolbar")
    
    ' Add three buttons to the new toolbar.
    ' All three buttons will have the same macro attached.
    Dim newButton1 As AcadToolbarItem
    Dim newButton2 As AcadToolbarItem
    Dim newButton3 As AcadToolbarItem
    Dim openMacro As String
    
    ' Assign the macro string the VB equivalent of "ESC ESC _open "
    openMacro = Chr(3) & Chr(3) & Chr(95) & "open" & Chr(32)
    
    Set newButton1 = newToolBar.AddToolbarButton("", "NewButton1", "Open a file.", openMacro)
    Set newButton2 = newToolBar.AddToolbarButton("", "NewButton2", "Open a file.", openMacro)
    Set newButton3 = newToolBar.AddToolbarButton("", "NewButton3", "Open a file.", openMacro)
    
    
    ' Display the toolbar
    newToolBar.Visible = True
    
    ' Dock the toolbar to the left of the screen.
    newToolBar.Dock acToolbarDockLeft
    MsgBox "The toolbar is docked to the left."
    
    ' Dock the toolbar to the right of the screen.
    newToolBar.Dock acToolbarDockRight
    MsgBox "The toolbar is docked to the right."
    
    ' Dock the toolbar at the top of the screen.
    newToolBar.Dock acToolbarDockTop
    MsgBox "The toolbar is docked at the top."
    
    ' Dock the toolbar at the bottom of the screen.
    newToolBar.Dock acToolbarDockBottom
    MsgBox "The toolbar is docked at the bottom."
    
End Sub
