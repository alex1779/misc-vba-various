Sub Example_ToleranceSuppressZeroFeet()
   ' This example creates an aligned dimension in model space and
   ' uses ToleranceSuppressZeroFeet to toggle the display of zeros feet
   ' in the dimension tolerance display

    Dim dimObj As AcadDimAligned
    Dim point1(0 To 2) As Double, point2(0 To 2) As Double
    Dim location(0 To 2) As Double
    
    ' Define the dimension
    point1(0) = 5: point1(1) = 5: point1(2) = 0
    point2(0) = 6.01: point2(1) = 5: point2(2) = 0
    location(0) = 5: location(1) = 8: location(2) = 0
    
    ' Create an aligned dimension object in model space
    Set dimObj = ThisDrawing.ModelSpace.AddDimAligned(point1, point2, location)
    dimObj.UnitsFormat = acDimLArchitectural
    
    ' Enable and setup tolerance display
    dimObj.ToleranceDisplay = acTolLimits
    dimObj.ToleranceLowerLimit = -0.0001: dimObj.ToleranceUpperLimit = 0.0001
    
    ZoomAll
    
Toggle:
    Select Case MsgBox("Press OK to toggle the display of zero feet in the dimension tolerance", vbOKCancel)
        Case vbOK
            ' Toggle the display of zero feet for the dimension tolerance value
            dimObj.ToleranceSuppressZeroFeet = Not (dimObj.ToleranceSuppressZeroFeet)
            ThisDrawing.Regen acAllViewports
        
        Case vbCancel
            Exit Sub
    End Select
    
    GoTo Toggle
End Sub
