Sub Example_SectionSolid()
    ' This example creates a box in model space.
    ' It then sections the box based on a plane
    ' defined by three points. The section is returned
    ' as a region.
        
    Dim boxObj As Acad3DSolid
    Dim length As Double, width As Double, height As Double
    Dim center(0 To 2) As Double
    
    ' Define the box
    center(0) = 5#: center(1) = 5#: center(2) = 0
    length = 5#: width = 7: height = 10#
    
    ' Create the box (3DSolid) object in model space
    Set boxObj = ThisDrawing.ModelSpace.AddBox(center, length, width, height)
    
    ' Define the section plane with three points
    Dim sectionPt1(0 To 2) As Double
    Dim sectionPt2(0 To 2) As Double
    Dim sectionPt3(0 To 2) As Double
    
    sectionPt1(0) = 1.5: sectionPt1(1) = 7.5: sectionPt1(2) = 0
    sectionPt2(0) = 1.5: sectionPt2(1) = 7.5: sectionPt2(2) = 10
    sectionPt3(0) = 8.5: sectionPt3(1) = 2.5: sectionPt3(2) = 10
    
    ' Section the box
    Dim sectionObj As AcadRegion
    Set sectionObj = boxObj.SectionSolid(sectionPt1, sectionPt2, sectionPt3)
    
    ' Change the viewing direction of the viewport
    Dim NewDirection(0 To 2) As Double
    NewDirection(0) = -1: NewDirection(1) = -1: NewDirection(2) = 1
    ThisDrawing.ActiveViewport.direction = NewDirection
    ThisDrawing.ActiveViewport = ThisDrawing.ActiveViewport
    ZoomAll
    
End Sub
