Sub Example_GradientColor2()
    ' This example changes the value of the GradientColor2 property.
    
    Dim hatchObj As AcadHatch
    Dim patternName As String
    Dim PatternType As Long
    Dim bAssociativity As Boolean
    
    ' Define the hatch
    patternName = "CYLINDER"
    PatternType = acPreDefinedGradient '0
    bAssociativity = True
    
    ' Create the associative Hatch object in model space
    Set hatchObj = ThisDrawing.ModelSpace.AddHatch(PatternType, patternName, bAssociativity, acGradientObject)
    Dim col1 As AcadAcCmColor, col2 As AcadAcCmColor, newColor As AcadAcCmColor
    Set col1 = AcadApplication.GetInterfaceObject("AutoCAD.AcCmColor.16")
    Set col2 = AcadApplication.GetInterfaceObject("AutoCAD.AcCmColor.16")
    Set newColor = AcadApplication.GetInterfaceObject("AutoCAD.AcCmColor.16")
    Call col1.SetRGB(255, 0, 0)
    Call col2.SetRGB(0, 255, 0)
    Call newColor.SetRGB(0, 100, 0)
    hatchObj.GradientColor1 = col1
    hatchObj.GradientColor2 = col2
    
    ' Create the outer boundary for the hatch (a circle)
    Dim outerLoop(0 To 0) As AcadEntity
    Dim center(0 To 2) As Double
    Dim radius As Double
    center(0) = 3: center(1) = 3: center(2) = 0
    radius = 1
    Set outerLoop(0) = ThisDrawing.ModelSpace.AddCircle(center, radius)
    
    ' Append the outerboundary to the hatch object, and display the hatch
    hatchObj.AppendOuterLoop (outerLoop)
    hatchObj.Evaluate
    ThisDrawing.Regen True
    MsgBox "Initial value of GradientColor2 is :" & vbCrLf & _
                    "red = " & hatchObj.GradientColor2.Red & vbCrLf & _
                    "green = " & hatchObj.GradientColor2.Green & vbCrLf & _
                    "blue = " & hatchObj.GradientColor2.Blue

    hatchObj.GradientColor2 = newColor
    MsgBox "New value of GradientColor2 is :" & vbCrLf & _
                    "red = " & hatchObj.GradientColor2.Red & vbCrLf & _
                    "green = " & hatchObj.GradientColor2.Green & vbCrLf & _
                    "blue = " & hatchObj.GradientColor2.Blue
End Sub
