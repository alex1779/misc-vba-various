Sub Example_ArrowHead2Type()
    ' This example creates a Dim3PointAngular object in model space
    ' and then alters the visible appearance (shape) of its arrowheads
    ' using the ArrowHead1Type and ArrowHead2Type properties
    
    Dim DimPointAngularObj As AcadDim3PointAngular
    Dim AngleVertex(0 To 2) As Double
    Dim FirstPoint(0 To 2) As Double, SecondPoint(0 To 2) As Double
    Dim TextPoint(0 To 2) As Double
    
    ' Define the new Dim3PointAngular object
    AngleVertex(0) = 0: AngleVertex(1) = 0: AngleVertex(2) = 0
    FirstPoint(0) = 2: FirstPoint(1) = 2: FirstPoint(2) = 0
    SecondPoint(0) = 1: SecondPoint(1) = 4: SecondPoint(2) = 0
    TextPoint(0) = 6: TextPoint(1) = 6: TextPoint(2) = 0

    ' Create the new Dim3PointAngular object in model space
    Set DimPointAngularObj = ThisDrawing.ModelSpace.AddDim3PointAngular(AngleVertex, FirstPoint, SecondPoint, TextPoint)
    ThisDrawing.Application.ZoomAll

    ' Read and display current arrowhead type
    MsgBox "The arrowhead type for the first arrow of this object is: " & DimPointAngularObj.Arrowhead1Type & vbCrLf & _
           "The arrowhead type for the second arrow of this object is: " & DimPointAngularObj.Arrowhead2Type

    ' Alter the arrowhead type property for both arrows of this object
    DimPointAngularObj.Arrowhead1Type = acArrowBoxBlank
    DimPointAngularObj.Arrowhead2Type = acArrowBoxBlank
    ThisDrawing.Regen acAllViewports
    
    ' Read and display current arrowhead type
    MsgBox "The arrowhead type for the first arrow of this object is now set to: " & DimPointAngularObj.Arrowhead1Type & vbCrLf & _
           "The arrowhead type for the second arrow of this object is now set to: " & DimPointAngularObj.Arrowhead2Type
End Sub
