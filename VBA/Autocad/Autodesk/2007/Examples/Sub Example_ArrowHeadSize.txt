Sub Example_ArrowHeadSize()
    ' This example creates a Dim3PointAngular object in model space
    ' and then alters the visible appearance (size) of its arrowheads
    ' using the ArrowHeadSize property
    
    Dim DimPointAngularObj As AcadDim3PointAngular
    Dim AngleVertex(0 To 2) As Double
    Dim FirstPoint(0 To 2) As Double, SecondPoint(0 To 2) As Double
    Dim TextPoint(0 To 2) As Double
    
    ' Define the new Dim3PointAngular object
    AngleVertex(0) = 0: AngleVertex(1) = 0: AngleVertex(2) = 0
    FirstPoint(0) = 2: FirstPoint(1) = 2: FirstPoint(2) = 0
    SecondPoint(0) = 1: SecondPoint(1) = 4: SecondPoint(2) = 0
    TextPoint(0) = 6: TextPoint(1) = 6: TextPoint(2) = 0

    ' Create the new Dim3PointAngular object in model space
    Set DimPointAngularObj = ThisDrawing.ModelSpace.AddDim3PointAngular(AngleVertex, FirstPoint, SecondPoint, TextPoint)
    ThisDrawing.Application.ZoomAll

    ' Read and display current arrowhead size
    MsgBox "The arrowhead size for this object is currently set to: " & DimPointAngularObj.ArrowheadSize

    ' Alter the arrowhead size property for this object
    DimPointAngularObj.ArrowheadSize = 0.5
    ThisDrawing.Regen acAllViewports
    
    ' Read and display new arrowhead size setting
    MsgBox "The arrowhead size for this object is now set to: " & DimPointAngularObj.ArrowheadSize
End Sub
