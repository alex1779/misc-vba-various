Sub Example_AltSuppressZeroFeet()
   ' This example creates an aligned dimension in model space and
   ' uses AltSuppressZeroFeet to suppress or view the display of
   ' zero feet in the alternate dimension display

    Dim dimObj As AcadDimAligned
    Dim point1(0 To 2) As Double, point2(0 To 2) As Double
    Dim location(0 To 2) As Double
    
    ' Define the dimension
    point1(0) = 0: point1(1) = 5: point1(2) = 0
    point2(0) = 5: point2(1) = 5: point2(2) = 0
    location(0) = 5: location(1) = 7: location(2) = 0
    
    ' Create an aligned dimension object in model space
    Set dimObj = ThisDrawing.ModelSpace.AddDimAligned(point1, point2, location)
    
    ' Enable alternate unit display
    dimObj.AltUnits = True
    
    ' Set the dimension display to a style that works with AltSuppressZeroFeet
    dimObj.AltUnitsFormat = acDimLArchitectural
    dimObj.AltUnitsScale = 2    ' Set the alternate unit scale to be a multiple of 2
    
    ThisDrawing.Application.ZoomAll
    
Toggle:
    Select Case MsgBox("Press OK to suppress or view the display of zero feet in the alternate dimension", vbOKCancel)
        Case vbOK
            ' Toggle the display of zero feet for the alternate dimension value
            dimObj.AltSuppressZeroFeet = Not (dimObj.AltSuppressZeroFeet)
            ThisDrawing.Regen acAllViewports
        Case vbCancel
            Exit Sub
    End Select
    
    GoTo Toggle
End Sub
